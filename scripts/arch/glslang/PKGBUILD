# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# Careful when upgrading this package! It usually breaks ABI without bumping soname.
pkgname=glslang
pkgver=7.11.3113
pkgrel=1
pkgdesc='OpenGL and OpenGL ES shader front end and validator'
arch=('x86_64')
url='https://github.com/KhronosGroup/glslang'
license=('BSD')
depends=('gcc-libs' 'spirv-tools' 'python')
makedepends=('cmake' 'ninja')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/glslang/archive/${pkgver}.tar.gz)
sha256sums=('4d238000162029f791cfcd65a28f10defa574516b94d9392695d27c8a1ce8b62')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build-{shared,static}
  (cd build-shared
    cmake .. \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=RelWithDebInfo \
      -DENABLE_HLSL=off \
      -DENABLE_NV_EXTENSIONS=ON \
      -DENABLE_AMD_EXTENSIONS=OFF \
      -DENABLE_SPV_REMAPPER=OFF \
      -DENABLE_GLSLANG_BINARIES=OFF \
      -DBUILD_SHARED_LIBS=ON
    ninja
  )
  (cd build-static
    cmake .. \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=RelWithDebInfo \
      -DENABLE_HLSL=off \
      -DENABLE_NV_EXTENSIONS=ON \
      -DENABLE_AMD_EXTENSIONS=OFF \
      -DENABLE_SPV_REMAPPER=OFF \
      -DENABLE_GLSLANG_BINARIES=OFF \
      -DBUILD_SHARED_LIBS=OFF
    ninja
  )
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build-shared install
  DESTDIR="${pkgdir}" ninja -C build-static install
  cd "${pkgdir}"/usr/lib
  for lib in *.so; do
    ln -sf "${lib}" "${lib}.0"
  done
}

# vim: ts=2 sw=2 et:
